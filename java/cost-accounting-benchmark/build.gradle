plugins {
    id 'java-library'
    id 'application'
    id 'com.diffplug.eclipse.apt' version '3.24.0'
    id 'com.github.spotbugs' version '4.7.1'
}

group = "com.tsurugidb.benchmark.cost-accounting-benchmark"
version = "1.1.0-SNAPSHOT"

if (hasProperty('mavenLocal')) {
    repositories {
        mavenLocal()
    }
} else {
    repositories {
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            content {
                includeGroupByRegex 'com\\.tsurugidb.*'
            }
        }
    }
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 8, 'hours'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

spotbugs {
  excludeFilter = file("spotbugs-exclude-filter.xml")
}

spotbugsMain {
    reports {
        html {
            enabled = !project.hasProperty("ci")
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            enabled = project.hasProperty("ci")
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
    }
}

spotbugsTest.enabled = false

dependencies {
  // https://mvnrepository.com/artifact/org.postgresql/postgresql
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.16'
  // https://mvnrepository.com/artifact/com.oracle.database.jdbc/ojdbc8
  runtimeOnly group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '21.1.0.0'
  implementation group: 'com.oracle.database.jdbc', name: 'ucp', version: '21.1.0.0'
  runtimeOnly group: 'com.oracle.database.nls', name: 'orai18n', version: '21.1.0.0'

  api 'com.tsurugidb.iceaxe:iceaxe-core:1.1.0-SNAPSHOT'

  // https://mvnrepository.com/artifact/org.apache.poi/poi
  implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
  // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'

  // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
  implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

  implementation "ch.qos.logback:logback-classic:1.2.3"

  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        showStandardStreams true
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat 'full'
    }
}

task showTsubakuroManifest {
    doLast {
        def tsubakuroJar = configurations["compileClasspath"].filter {
            it.name.startsWith("tsubakuro-session")
        }
        def version = resources.text.fromArchiveEntry(tsubakuroJar, "META-INF/MANIFEST.MF")
        print(version.asString())
    }
}

task showIceaxeManifest {
    doLast {
        def iceaxeJar = configurations["compileClasspath"].filter {
            it.name.startsWith("iceaxe-core")
        }
        def version = resources.text.fromArchiveEntry(iceaxeJar, "META-INF/MANIFEST.MF")
        print(version.asString())
    }
}

startScripts {
    applicationName = 'costaccounting'
    mainClass = 'com.tsurugidb.benchmark.costaccounting.Main'
    applicationDefaultJvmArgs = ['-Dcom.tsurugidb.tsubakuro.jniverify=false']
}

distTar {
    archiveFileName = "${project.name}.tar.gz"
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}
